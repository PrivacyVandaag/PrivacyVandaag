apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.2"

    signingConfigs {
        release {
            if (project.hasProperty("KEYSTORE_PATH")) {
                storeFile file(KEYSTORE_PATH)
                storePassword KEYSTORE_PASSWORD
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
                // v2SigningEnabled false
            }
        }
    }
    defaultConfig {
        applicationId "nl.privacybarometer.privacyvandaag"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 161
        versionName "2.5.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }
    flavorDimensions "version"
    productFlavors {
        // This is the build variant for distibution outside Google Play store
        // The main difference is that is has an built-in update function in the AboutActivity class.
        floss {
            applicationId 'nl.privacybarometer.privacyvandaag'
            buildConfigField "String", "PRODUCT_FLAVOR", "\"floss\""
        }
        // This is the build variant for the distribution through Google Play Store
        // Since apps are updated through Google App, this variant has no update function in AboutActivity class
        // Instead, we add 'play-services' in the dependencies.
        gplay {
            applicationId 'nl.privacybarometer.privacyvandaag.gplay'
            buildConfigField "String", "PRODUCT_FLAVOR", "\"gplay\""
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-beta01', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.core:core:1.0.0' /* Needed at least for notifications. Probably not necessary to call it here separately, but just to be sure. */
    implementation 'androidx.media:media:1.0.0' /* somewhere there exists an old dependency, so we call this explicitly */
    implementation 'androidx.legacy:legacy-support-v4:1.0.0' /* somewhere there exists an old dependency, so we call this explicitly */
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'com.google.android.material:material:1.0.0' // necessary for Floatin Action Button

    // Needed for the new Architecture Components by google.
    def lifecycle_version = "2.0.0"
    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"


    // Play Store and Play Services
    gplayImplementation 'com.google.android.gms:play-services-gcm:16.0.0'  // Only needed for gplay release


    // For updating SSL security patches. Used in NetworkUtils.java
    implementation 'com.google.android.gms:play-services-auth:16.0.1'

    // Search on package name, see what latest available version is and use those version numbers.
    implementation 'org.jsoup:jsoup:1.10.2'     // Necessary to make selections in the DOM of a webpage to cut out the main article and images.
    implementation 'com.squareup.picasso:picasso:2.5.2'    // Necessary to transform / cut images.

    androidTestImplementation 'junit:junit:4.12'
}
